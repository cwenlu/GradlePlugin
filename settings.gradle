rootProject.name = "GradlePlugin"
include ':app'

//初始化阶段开始时间
long beginOfSetting = System.currentTimeMillis()

//配置阶段开始时间
def beginOfConfig

//配置阶段是否开始
def configHasBegin = false

//每个配置(build.gradle)执行前的时间
def beginOfProjectConfig = new HashMap()

//执行阶段开始时间
def beginOfTaskExecute

//初始化阶段执行完毕回调(执行settings.gradle 脚本，构建完 Project 对象)
gradle.projectsEvaluated {
    println("初始化耗时 ${System.currentTimeMillis() - beginOfSetting} ms")
}

//build.gradle 执行前
gradle.beforeProject { Project project ->
    if (!configHasBegin) {
        configHasBegin = true
        beginOfConfig = System.currentTimeMillis()
    }
    beginOfProjectConfig.put(project, System.currentTimeMillis())
}

//build.gradle 执行后
gradle.afterProject { Project project ->
    def begin = beginOfProjectConfig.get(project)
    println("配置阶段 ${project} 耗时：${System.currentTimeMillis() - begin} ms")
}

//配置阶段完毕
gradle.taskGraph.whenReady {
    beginOfTaskExecute = System.currentTimeMillis()
    println("配置阶段总耗时：${System.currentTimeMillis() - beginOfConfig}")
}

//执行阶段
gradle.taskGraph.beforeTask { Task task ->
    task.doFirst {
        task.ext.beginOfTask = System.currentTimeMillis()
    }

    task.doLast {
        println("执行阶段 ${task} 耗时：${System.currentTimeMillis() - task.ext.beginOfTask}")
    }
}

//执行完毕
gradle.buildFinished {
    println("执行阶段总耗时:${System.currentTimeMillis() - beginOfTaskExecute}")
}
